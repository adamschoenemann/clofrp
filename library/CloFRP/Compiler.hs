{-# LANGUAGE BangPatterns #-}
{-# LANGUAGE GADTs #-}
{-# LANGUAGE LambdaCase #-}
{-# LANGUAGE TemplateHaskell #-}
{-# LANGUAGE QuasiQuotes #-}
{-# LANGUAGE FlexibleContexts #-}
{-# LANGUAGE KindSignatures #-}
{-# LANGUAGE DataKinds #-}
{-# LANGUAGE RecordWildCards #-}
{-# LANGUAGE DeriveFunctor #-}

-- compile CloFRP programs to Haskell (denotational semantics)
module CloFRP.Compiler where

import qualified Language.Haskell.TH.Syntax as TH
import Language.Haskell.TH.Syntax (Q, Dec)
import Language.Haskell.TH.Lib
import Data.Maybe (mapMaybe)
import Data.Functor.Classes

import CloFRP.AST hiding (match, Prim(..))
import qualified CloFRP.AST.Prim as P
import CloFRP.Annotated

data ClockKind = K0

type Later (k :: ClockKind) a = () -> a

data StreamF' (k :: ClockKind) a f = Cons' !a !(Later k f) deriving Functor

unguard :: ((() -> a) -> a) -> a -> a
unguard f x = f (\_ -> x)

gfix :: (Later k a -> a) -> a
gfix f = -- fix (unguard f)
  let x = f (\_ -> x)
  in  x

-- F[ŒºX. F[X]] -> (ŒºX. F[X])
data Fix f = Fold (f (Fix f))

instance Show1 f => Show (Fix f) where
  showsPrec d (Fold a) =
    showParen (d >= 11)
      $ showString "Fold "
      . showsPrec1 11 a

-- (ŒºX. F[X]) -> F[ŒºX. F[X]]
unfold :: Fix f -> f (Fix f)
unfold (Fold f) = f

-- primitive recursion
-- (F[(¬µX. F) √ó A] ‚Üí A) ‚Üí ¬µX. F[X] ‚Üí A
primRec :: Functor f => (f (Fix f, a) -> a) -> Fix f -> a
primRec fn (Fold f) =
  fn (fmap (\y -> (y, primRec fn y)) f)

nameToName :: Name -> TH.Name
nameToName = TH.mkName . show

progToHaskDecls :: Prog a -> Q [Dec]
progToHaskDecls (Prog decls) = sequence (mapMaybe declToHaskDecl decls) where
  declToHaskDecl (A _ decl') =
    case decl' of
      FunD nm e    -> Just $ valD (varP (nameToName nm)) (normalB $ exprToHaskExpQ e) []
      SigD nm ty   -> Just $ sigD (nameToName nm) (typeToHaskType ty)
      DataD dt     -> dtToHaskDt dt
      SynonymD syn -> Just $ synToHaskSyn syn

synToHaskSyn :: Synonym a -> DecQ
synToHaskSyn (Synonym {synName = nm, synBound = bnd, synExpansion = ex}) =
  tySynD (nameToName nm) (bndrsToHaskBndrs bnd) (typeToHaskType ex)

bndrsToHaskBndrs :: [(Name, Kind)] -> [TH.TyVarBndr]
bndrsToHaskBndrs = map (\(n,k) -> kindedTV (nameToName n) (kindToHaskKind k))

dtToHaskDt :: Datatype a -> Maybe DecQ
dtToHaskDt (Datatype { dtName = nm, dtExtern = ex, .. })
  | ex = Nothing -- extern data-types does not generate Haskell syntax
  | otherwise = Just $ dataD ctx (nameToName nm) bound kind constrs derivs where
      ctx = pure []
      bound = bndrsToHaskBndrs dtBound
      kind  = Nothing
      constrs = map constrToHaskConstr dtConstrs
      derivs = map derivToHaskDeriv dtDeriving

derivToHaskDeriv :: String -> DerivClauseQ
derivToHaskDeriv clazz = derivClause Nothing [conT (TH.mkName clazz)]

constrToHaskConstr :: Constr a -> ConQ
constrToHaskConstr (A _ (Constr nm args)) = normalC (nameToName nm) (map argToBangTypeQ args) where
  argToBangTypeQ typ =
    bangType (bang noSourceUnpackedness sourceStrict) (typeToHaskType typ)

typeToHaskType :: PolyType a -> TypeQ
typeToHaskType = go where
  go (A _ typ') = case typ' of
    TFree x       -> conT (nameToName x)                  -- ‚ü¶ùíØ‚üß = ùíØ
    TVar x        -> varT (nameToName x)                  -- ‚ü¶x‚üß = x
    TExists x     -> error $ "cannot compile existential " ++ show x
    t1 `TApp` t2  -> go t1 `appT` go t2                   -- ‚ü¶t1 t2‚üß = ‚ü¶t1‚üß ‚ü¶t2‚üß
    t1 :->: t2    -> arrowT `appT` go t1 `appT` go t2     -- ‚ü¶t1 -> t2‚üß = ‚ü¶t1‚üß -> ‚ü¶t2‚üß
    Forall nm k tau ->                                    -- ‚ü¶forall (a : k). t‚üß = forall (a :: k) ‚ü¶t‚üß
      let b = kindedTV (nameToName nm) (kindToHaskKind k)
      in  forallT [b] (pure []) (go tau)
    RecTy tau     -> conT ''Fix `appT` go tau             -- ‚ü¶Fix t‚üß = Fix ‚ü¶t‚üß
    TTuple ts     ->                                      -- ‚ü¶(t1, ..., tn)‚üß = (‚ü¶t1‚üß, ..., ‚ü¶tn‚üß)
      foldl (\acc x -> acc `appT` go x) (tupleT (length ts)) ts
    Later k t     -> conT ''Later `appT` go k `appT` go t -- ‚ü¶‚ñ∑k t‚üß = Later ‚ü¶k‚üß ‚ü¶t‚üß

kindToHaskKind :: Kind -> TH.Kind
kindToHaskKind = go where
  go = \case
    Star -> starK
    k1 :->*: k2 -> arrowK `appK` go k1 `appK` go k2
    ClockK -> conK ''ClockKind


exprToHaskExpQ :: Expr a -> ExpQ
exprToHaskExpQ = go where
  go (A _ expr') = case expr' of
    Var nm
      | isConstrNm nm  -> conE (nameToName nm)               -- ‚ü¶ùíû‚üß = ùíû
      | otherwise      -> varE (nameToName nm)               -- ‚ü¶x‚üß = x
    TickVar nm         -> varE (nameToName nm)               -- ‚ü¶[x]‚üß = x
    Ann e t            -> sigE (go e) (typeToHaskType t)     -- ‚ü¶e : t‚üß = e :: t
    App e1 e2          -> appE (go e1) (go e2)               -- ‚ü¶e1 e2‚üß = ‚ü¶e1‚üß ‚ü¶e2‚üß
    Lam nm mty e       ->                                    -- ‚ü¶Œª(x : t). e‚üß = Œª(!x :: ‚ü¶t‚üß). ‚ü¶e‚üß
      lamE [lamArg (nameToName nm) (typeToHaskType <$> mty)] (go e)
    TickAbs nm kappa e ->                                    -- ‚ü¶Œ≥(x : k). e‚üß = Œª!x. ‚ü¶e‚üß
      lamE [lamArg (nameToName nm) (Just $ conT ''())] (go e)
    Tuple es           -> tupE (map go es)                   -- ‚ü¶(e‚ÇÅ, ..., e‚Çô)‚üß = (‚ü¶e‚ÇÅ‚üß, ..., ‚ü¶e‚Çô‚üß)
    Let p e1 e2        ->                                    -- ‚ü¶let p = e1 in e2‚üß = let ‚ü¶p‚üß = ‚ü¶e1‚üß in ‚ü¶e2‚üß
      let clause = valD (patToHaskPatQ p) (normalB (exprToHaskExpQ e1)) []
      in  letE [clause] (go e2)
    Case e clauses     ->                                    -- ‚ü¶case e of cs‚üß = case ‚ü¶e‚üß of ‚ü¶cs‚üß
      caseE (go e) (clausesToMatchQ clauses)
    TypeApp e t        -> appTypeE (go e) (typeToHaskType t) -- ‚ü¶e {t}‚üß = ‚ü¶e‚üß @‚ü¶t‚üß
    Fmap t             -> varE 'fmap                         -- ‚ü¶$\mathtt{fmap}_F$‚üß = fmap
    PrimRec t          -> varE 'primRec                      -- ‚ü¶primRec‚üß = primRec
    BinOp nm e1 e2     ->                                    -- ‚ü¶op e1 e2‚üß = (‚ü¶op‚üß) ‚ü¶e1‚üß ‚ü¶e2‚üß
      varE (nameToName nm) `appE` go e1 `appE` go e2
    Prim p             -> primToHaskExprQ p

  lamArg nm mty =
    maybe (bangP (varP nm)) (bangP . sigP (varP nm)) mty

clausesToMatchQ :: [(Pat a, Expr a)] -> [MatchQ]
clausesToMatchQ = foldr clauseToMatchQ [] where
  clauseToMatchQ (p, e) acc = match (patToHaskPatQ p) (normalB (exprToHaskExpQ e)) [] : acc

patToHaskPatQ :: Pat a -> PatQ
patToHaskPatQ  = go where
  go (A _ pat') = case pat' of
    Bind nm -> varP (nameToName nm)
    Match nm pats -> conP (nameToName nm) (map go pats)
    PTuple ps -> tupP (map go ps)

primToHaskExprQ :: P.Prim -> ExpQ
primToHaskExprQ = \case
  P.Unit           -> conE '()
  P.Integer x      -> litE (integerL x)
  P.Fold           -> conE 'Fold
  P.Unfold         -> varE 'unfold
  P.Tick           -> conE '()
  P.Fix            -> varE 'gfix
  P.Undefined      -> varE 'undefined
  P.Input          -> error "cannot compile Input"
